# 2022/10/31
# zhangzhong
# copy.ys: Copy a source block to a destination block

# Execution begins at address 0
.pos 0
    irmovq stack, %rsp  # Set up stack pinter
    call main           # Execute main function
    halt                # Terminate Program

.align 8
# source block
src:
    .quad 0x00a
    .quad 0x0b0
    .quad 0xc00

# destination block
dest:
    .quad 0x111
    .quad 0x222
    .quad 0x333

main:
    irmovq src, %rdi
    irmovq dest, %rsi
    irmovq $3, %rdx
    call copy_block
    ret

copy_block:
    xorq %rcx, %rcx         # result = 0
    jmp L12
L13:
    mrmovq (%rdi), %rax     # val = *src
    rmmovq %rax, (%rsi)     # *dest = val
    xorq %rax, %rcx         # result ^= val

    irmovq $8, %r8
    addq %r8, %rdi          # src++
    addq %r8, %rsi          # dest++

    irmovq $1, %r8          
    subq %r8, %rdx          # len--
L12:
    andq %rdx, %rdx     # len > 0
    jg L13
    rrmovq %rcx, %rax
    ret


# Stack starts here and grows to lower addresses
	.pos 0x200
stack:
